// Copyright 2018 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

apply plugin: 'org.jetbrains.intellij'

intellij {
    version "IC-" + ideaVersion
    pluginName 'aws-intellij-toolkit'
    updateSinceUntilBuild false
    downloadSources = System.getenv("CI") == null
    plugins = ["PythonCore:2018.2.182.3684.101", "yaml"]
}

jar.baseName = 'aws-intellij-toolkit-core'

configurations {
    testArtifacts
}

task testJar (type: Jar) {
    baseName = "${project.name}-test"
    from sourceSets.test.output
}

task pluginChangeLog(type: GenerateChangeLog) {
    changesDirectory = project.parent.file(".changes")
    includeUnreleased = true
    generateGithub = false
    issuesUrl = "https://github.com/aws/aws-toolkit-jetbrains/issues"
    jetbrainsChangeNotesFile = project.file("$buildDir/resources/main/META-INF/change-notes.xml")
}

processResources.dependsOn(pluginChangeLog)

artifacts {
    testArtifacts testJar
}

dependencies {
    compile(project(":core")) {
        exclude group: 'org.slf4j'
    }
    compile("software.amazon.awssdk:s3:$awsSdkVersion") {
        exclude group: 'org.slf4j'
    }
    compile("software.amazon.awssdk:lambda:$awsSdkVersion") {
        exclude group: 'org.slf4j'
    }
    compile("software.amazon.awssdk:iam:$awsSdkVersion") {
        exclude group: 'org.slf4j'
    }
    compile("software.amazon.awssdk:cloudformation:$awsSdkVersion") {
        exclude group: 'org.slf4j'
    }
    testCompile project(path: ":core", configuration: 'testArtifacts')
    testCompile project(":jetbrains-testutils")
}