// Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

def protocolGroup = 'protocol'

// TODO: Remove and fix clean task
ext.csDaemonGeneratedOutput = new File(resharperPluginPath, "src/AWS.Daemon/Protocol")
ext.csPsiGeneratedOutput = new File(resharperPluginPath, "src/AWS.Psi/Protocol")
ext.csAwsSettingGeneratedOutput = new File(resharperPluginPath, "src/AWS.Settings/Protocol")

ext.ktGeneratedOutput = new File(projectDir, "src/software/aws/toolkits/jetbrains/protocol")

ext.modelDir = new File(projectDir, "protocol/model")

task generateModel(type: tasks.getByName("rdgen").class) {
    group = protocolGroup
    description = 'Generates protocol models'

    // NOTE: classpath is evaluated lazily, at execution time, because it comes from the unzipped
    // intellij SDK, which is extracted in afterEvaluate
    params {
        verbose = true
        hashFolder = "${project.buildDir}/rdgen/"

        logger.info("Configuring rdgen params")
        classpath {
            logger.info("Calculating classpath for rdgen, intellij.ideaDependency is: ${intellij.ideaDependency}")
            def sdkPath = intellij.ideaDependency.classes
            def rdLibDirectory = new File(sdkPath, "lib/rd").canonicalFile

            "$rdLibDirectory/rider-model.jar"
        }
        sources modelDir.canonicalPath
        packages = "protocol.model"

        properties["ktGeneratedOutput"]="${projectDir.canonicalPath}/src/software/aws/toolkits/jetbrains/protocol"

        properties["csDaemonGeneratedOutput"]="${resharperPluginPath.canonicalPath}/src/AWS.Daemon/Protocol"
        properties["csPsiGeneratedOutput"]="${resharperPluginPath.canonicalPath}/src/AWS.Psi/Protocol"
        properties["csAwsSettingGeneratedOutput"]="${resharperPluginPath.canonicalPath}/src/AWS.Settings/Protocol"
    }
}

task cleanProtocolModels {
    group = protocolGroup
    description = 'Clean up generated protocol models'

    def protocolOutDirs = [ ktGeneratedOutput, csDaemonGeneratedOutput, csPsiGeneratedOutput, csAwsSettingGeneratedOutput ]

    for (dir in protocolOutDirs) {
        if (dir.isDirectory()) {
            dir.deleteDir()
        }
    }
}

jar.dependsOn generateModel
