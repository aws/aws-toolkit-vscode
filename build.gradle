// Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:$ideaPluginVersion"
        classpath "com.adarshr:gradle-test-logger-plugin:1.7.0"
    }
}

apply from: 'intellijJVersions.gradle'

group 'software.aws.toolkits'
// please check changelog generation logic if this format is changed
version toolkitVersion + "-" + shortenVersion(resolveIdeProfileName())

repositories {
    maven { url "https://www.jetbrains.com/intellij-repository/snapshots/" }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    apply plugin: "com.adarshr.test-logger"
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(JavaExec) {
        systemProperty("aws.toolkits.enableTelemetry", false)
    }

    tasks.withType(org.jetbrains.intellij.tasks.RunIdeTask) {
        systemProperty("aws.toolkits.enableTelemetry", false)
        intellij {
            if (System.env.ALTERNATIVE_IDE) {
                if(file(System.env.ALTERNATIVE_IDE).exists()) {
                    alternativeIdePath = System.env.ALTERNATIVE_IDE
                } else {
                    throw new GradleException("ALTERNATIVE_IDE path not found"
                      + (System.env.ALTERNATIVE_IDE ==~ /.*[\/\\] *$/
                        ? " (HINT: remove trailing slash '/')"
                        : ": ${System.env.ALTERNATIVE_IDE}"))
                }
            }
        }
    }

    configurations {
        runtimeClasspath.exclude group: "org.slf4j"
        runtimeClasspath.exclude group: "org.jetbrains.kotlin"
        runtimeClasspath.exclude group: "org.jetbrains.kotlinx"
    }
}

// Kotlin plugin seems to be bugging out when there are no kotlin sources
configure(subprojects - project(":telemetry-client")) {
    apply plugin: 'kotlin'

    sourceSets {
        integrationTest {
            kotlin.srcDir 'it'
        }
    }
}

subprojects {
    group = parent.group
    version = parent.version

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'signing'

    def isReleaseVersion = !version.endsWith("SNAPSHOT")

    signing {
        required { isReleaseVersion && gradle.startParameter.taskNames.contains("publishPlugin") }
        sign configurations.archives
    }

    sourceSets {
        main.java.srcDir 'src'
        main.resources.srcDir 'resources'
        test.java.srcDir 'tst'
        test.resources.srcDir 'tst-resources'
        integrationTest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            java.srcDir 'it'
            resources.srcDir 'it-resources'
        }
    }

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion"
        testImplementation "org.assertj:assertj-core:$assertjVersion"
        testImplementation "junit:junit:$junitVersion"
    }

    testlogger {
        showFullStackTraces true
        showStandardStreams true
        showPassedStandardStreams false
        showSkippedStandardStreams true
        showFailedStandardStreams true
    }

    test {
        jacoco {
            // don't instrument sdk, icons, ktlint, etc.
            includes = ["software.aws.toolkits.*"]
            excludes = ["software.aws.toolkits.ktlint"]
        }

        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    idea {
        module {
            sourceDirs -= file("it")
            testSourceDirs += file("it")

            resourceDirs -= file("it-resources")
            testResourceDirs += file("it-resources")
        }
    }

    task integrationTest(type: Test) {
        group = LifecycleBasePlugin.VERIFICATION_GROUP
        description = "Runs the integration tests."
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath

        jacoco {
            excludes = ["com.sun.*"] // See https://groups.google.com/forum/#!topic/jacoco/H0gDwxNuhK4
        }

        mustRunAfter tasks.test
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions.jvmTarget = "1.8"
    }

    // Force us to compile the integration tests even during check even though we don't run them
    check.dependsOn(integrationTestClasses)

    // Remove the tasks added in by gradle-intellij-plugin so that we don't publish/verify multiple times
    project.afterEvaluate {
        removeTask(tasks, 'publishPlugin')
        removeTask(tasks, 'verifyPlugin')
        removeTask(tasks, 'buildSearchableOptions')
        if (!isReleaseVersion) {
            removeTask(tasks, "signArchives")
        }
    }
}

configurations {
    ktlint
}

def removeTask(tasks, taskName) {
    def task = tasks.findByName("$taskName")

    if (task != null) {
        task.setEnabled(false)
    }
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: ChangeLogPlugin

intellij {
    version ideSdkVersion("IC")
    pluginName 'aws-jetbrains-toolkit'
    updateSinceUntilBuild false
    downloadSources = System.getenv("CI") == null
}

prepareSandbox {
    tasks.findByPath(":jetbrains-rider:buildReSharperPlugin")?.collect {
        from(it, {
            into("${intellij.pluginName}/dotnet")
        })
    }
}

publishPlugin {
    token publishToken
    channels publishChannel
}

generateChangeLog {
    generateJetbrains = false
    generateGithub = true
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"

    def isWindows = System.properties['os.name'].toLowerCase().contains('windows')

    def toInclude = project.rootDir.relativePath(project.projectDir) + "/**/*.kt"
    def toExclude = project.rootDir.relativePath(new File(project.projectDir, "jetbrains-rider")) + "/**/*.Generated.kt"

    if (isWindows) {
        toInclude = toInclude.replace("/", "\\")
        toExclude = toExclude.replace("/", "\\")
    }

    args "-v", toInclude, "!${toExclude}", "!/**/generated-src/**/*.kt"

    inputs.files(project.fileTree(dir: ".", include: "**/*.kt"))
    outputs.dir("${project.buildDir}/reports/ktlint/")
}
check.dependsOn ktlint
check.dependsOn verifyPlugin

task coverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    getAdditionalSourceDirs().from(subprojects.sourceSets.main.java.srcDirs)
    getSourceDirectories().from(subprojects.sourceSets.main.java.srcDirs)
    getClassDirectories().from(subprojects.sourceSets.main.output.classesDirs)

    reports {
        html.enabled true
        xml.enabled true
    }
}
subprojects.forEach {
    coverageReport.mustRunAfter(it.tasks.withType(Test))
}
check.dependsOn coverageReport

// Workaround for runIde being defined in multiple projects, if we request the root project runIde, "alias" it to
// community edition
if (gradle.startParameter.taskNames.contains("runIde")) {
    // Only disable this if running from root project
    if (project.projectDir == project.rootProject.rootDir
        || System.properties.containsKey("idea.gui.tests.gradle.runner")) {
        println("Top level runIde selected, excluding sub-projects' runIde")
        gradle.taskGraph.whenReady { graph ->
            graph.allTasks.forEach {
                if (gradle.startParameter.systemPropertiesArgs.getOrDefault("exec.args", "").contains("guitest")) {
                    if (it.name == "runIde" && it.project != project(':jetbrains-core-gui')) {
                        it.enabled = false
                    }
                } else {
                    if (it.name == "runIde" && it.project != project(':jetbrains-core')) {
                        it.enabled = false
                    }
                }
            }
        }
    }
}

task guiTest(type: Test) {
    dependsOn ":jetbrains-core-gui:guiTest"
}

dependencies {
    implementation project(':jetbrains-ultimate')
    project.findProject(':jetbrains-rider')?.collect {
        implementation it
    }

    ktlint "com.pinterest:ktlint:$ktlintVersion"
    ktlint project(":ktlint-rules")
}
